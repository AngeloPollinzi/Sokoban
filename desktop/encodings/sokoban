step(0..28).
maxmosse(28).

%configurazione iniziale.
target(X,Y):- cell(X,Y,4).
playerposition(0,X,Y):-cell(X,Y,1).
boxposition(0,Id,X,Y):-box(Id,X,Y).

%decido se spostare un box o meno in una certa casella se ci sono ancora mosse disponibili.
move(S,Id,X2,Y2)|not_move(S,Id,X2,Y2):-boxposition(S,Id,X1,Y1),adj(X1,Y1,X2,Y2),step(S),maxmosse(M),S<M.

%non posso spostare piu' di un box nello stesso step.
:-move(S,Id1,_,_),move(S,Id2,_,_),Id1!=Id2.
%non posso spostare un box in due punti diversi nello stesso istante.
:- move(S,Id,X,Y1),move(S,Id,X,Y2),Y1!=Y2.
:- move(S,Id,X1,Y),move(S,Id,X2,Y),X1!=X2.
:- move(S,Id,X1,Y1),move(S,Id,X2,Y2),X1!=X2,Y1!=Y2.
%evito un doppio spostamento poco sensato(ottimizzazione). 
:- boxposition(S,Id,X,Y),boxposition(S+1,Id,X,Y+1),boxposition(S+2,Id,X,Y-1).
:- boxposition(S,Id,X,Y),boxposition(S+1,Id,X,Y-1),boxposition(S+2,Id,X,Y+1).
:- boxposition(S,Id,X,Y),boxposition(S+1,Id,X+1,Y),boxposition(S+2,Id,X-1,Y).
:- boxposition(S,Id,X,Y),boxposition(S+1,Id,X-1,Y),boxposition(S+2,Id,X+1,Y).

%predicato che mi indica se ad un certo step muovo qualcosa.
somethingmoved(S):-move(S,_,_,_).

%non e' possibile che ad un certo step non muovo qualcosa.
:-step(S),not maxmosse(S),not somethingmoved(S).

%le posizioni occupate da un box.
occupied(S,X,Y) :- boxposition(S,_,X,Y).

%le box spostate. 
pushedbox(S,Id):-boxposition(S,Id,X1,Y1),boxposition(S+1,Id,X2,Y2),adj(X1,Y1,X2,Y2).

%tutti i vari spostamenti di un box che posso fare in ogni istante.
boxposition(S+1,Id,X,Y):-move(S,Id,X,Y).

%in base alla direzione scelta cambio la posizione del player,considerando che dove prima c'era il box 
%ora c'e' il player e che abbia spostato effettivamente qualcosa.
playerposition(S+1,X,Y):- move(S,Id,_,_),boxposition(S,Id,X,Y).

%caso in cui ad uno step un box non venga mosso.
boxposition(S+1,Id,X,Y) :- boxposition(S,Id,X,Y), not pushedbox(S,Id),maxmosse(M),step(S),S<M.
playerposition(S+1,X,Y) :- playerposition(S,X,Y),not somethingmoved(S),maxmosse(M),step(S),S<M.

%al termine non puo' esistere un box che non sia su un target. 
reached(Id) :- maxmosse(M),boxposition(M,Id,X,Y),target(X,Y).
:-boxposition(_,Id,_,_),not reached(Id).

%identifica la raggiungibilita' di un punto da parte del player ad ogni step.
reachable(S,X,Y) :- playerposition(S,X,Y),step(S).
reachable(S,X2,Y2) :- reachable(S,X1,Y1),adj(X1,Y1,X2,Y2),not occupied(S,X2,Y2).

%mi trovo tutti i punti diversi da un dead point, ossia quei punti che hanno una posizione valida a destra e sinistra 
%oppure sopra o sotto ad eccezione dei target.
notdeadpoint(X,Y) :- target(X,Y).
notdeadpoint(X,Y) :- adj(X,Y,X+1,Y),adj(X,Y,X-1,Y).
notdeadpoint(X,Y) :- adj(X,Y,X,Y+1),adj(X,Y,X,Y-1).

%non posso spostare un box in una cella non occupata da un altro box che sia un punto morto.
:-move(S,Id,X,Y),not notdeadpoint(X,Y).

%non posso spostare un box dove ce n'e' gia' un altro.
:-move(S,Id,X,Y),occupied(S,X,Y).

%non posso spostare un box in una certa direzione se il player non riesce a raggiungere 
%la casella dalla quale spingerlo.
:-boxposition(S,Id,X,Y),boxposition(S+1,Id,X,Y+1),not reachable(S,X,Y-1).
:-boxposition(S,Id,X,Y),boxposition(S+1,Id,X,Y-1),not reachable(S,X,Y+1).
:-boxposition(S,Id,X,Y),boxposition(S+1,Id,X+1,Y),not reachable(S,X-1,Y).
:-boxposition(S,Id,X,Y),boxposition(S+1,Id,X-1,Y),not reachable(S,X+1,Y).

%e' preferibile spostare per piu' step successivi sempre lo stesso box.
:~move(S,Id1,_,_),move(S+1,Id2,_,_),Id1!=Id2. [1@1]
